/*
 * Copyright (c) 2017 52inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

project.archivesBaseName = ARTIFACT_ID

install {
    repositories.mavenInstaller {
        pom {
            project {
                name POM_NAME
                description POM_DESCRIPTION
                url POM_URL
                inceptionYear POM_YEAR

                packaging POM_PACKAGING
                groupId GROUP
                artifactId ARTIFACT_ID
                version VERSION_CODE

                licenses {
                    license {
                        name POM_LICENCE_NAME
                        url POM_LICENCE_URL
                        distribution POM_LICENCE_DIST
                    }
                }

                developers {
                    developer {
                        id POM_DEVELOPER_ID
                        name POM_DEVELOPER_NAME
                        email POM_DEVELOPER_EMAIL
                    }
                }

                scm {
                    url POM_SCM_URL
                    connection POM_SCM_CONNECTION
                    developerConnection POM_SCM_DEV_CONNECTION
                }
            }
        }
    }
}

bintray {
    user = FTINC_BINTRAY_USER
    key = FTINC_BINTRAY_API_KEY
    configurations = ['archives']
    pkg {
        repo = 'CanvasScript'
        name = 'CanvasScript'
        userOrg = '52inc'
        licenses = ['Apache-2.0']
        vcsUrl = POM_URL
        version {
            name = VERSION_CODE
            desc = VERSION_DESC
            released = new Date()
            vcsTag = "v$VERSION_CODE"
        }
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

afterEvaluate {
    androidJavadocs.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompile.classpath.files
    })
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

task publishArchives {
    dependsOn 'clean'
    dependsOn 'install'
    dependsOn 'bintrayUpload'
    tasks.findByName('install').mustRunAfter 'clean'
    tasks.findByName('bintrayUpload').mustRunAfter 'install'
}